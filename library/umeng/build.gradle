plugins {
    id 'com.android.library'
}
apply from : '../../common.gradle'

android {
    namespace 'com.hjq.umeng'

    buildFeatures {
        // 是否生成 BuildConfig 类
        buildConfig true
    }

    defaultConfig {
        // 模块混淆配置
        consumerProguardFiles 'proguard-umeng.pro'

        // 构建配置字段
        buildConfigField('String', 'UM_KEY', '\"' + UMENG_APP_KEY + '\"')
        buildConfigField('String', 'QQ_ID', '\"' + QQ_APP_ID + '\"')
        buildConfigField('String', 'QQ_SECRET', '\"' + QQ_APP_SECRET + '\"')
        buildConfigField('String', 'WX_ID', '\"' + WX_APP_ID + '\"')
        buildConfigField('String', 'WX_SECRET', '\"' + WX_APP_SECRET + '\"')

        // 清单占位符
        addManifestPlaceholders([
                'UM_KEY' : UMENG_APP_KEY,
                'QQ_ID' : QQ_APP_ID,
                'QQ_SECRET' : QQ_APP_SECRET,
                'WX_ID' : WX_APP_ID,
                'WX_SECRET' : WX_APP_SECRET
        ])
    }
}

// 友盟统计集成文档：https://developer.umeng.com/docs/119267/detail/118584
// 友盟社会化集成文档：https://developer.umeng.com/docs/128606/detail/193879
// 友盟远程仓库地址：https://repo1.maven.org/maven2
dependencies {
    // 友盟公共库
    api 'com.umeng.umsdk:common:9.8.8'
    api 'com.umeng.umsdk:asms:1.8.7.2'
    // 友盟分享库
    api 'com.umeng.umsdk:share-core:7.3.7'
    // 友盟微信分享
    api 'com.umeng.umsdk:share-wx:7.3.7'
    // 友盟 QQ 分享
    api 'com.umeng.umsdk:share-qq:7.3.7'

    // QQ 组件：https://wikinew.open.qq.com/#/iwiki/4007776153
    api 'com.tencent.qq:open-sdk:3.5.18.0'
    // 微信组件：https://developers.weixin.qq.com/doc/oplatform/Mobile_App/Access_Guide/Android.html
    api 'com.tencent.mm.opensdk:wechat-sdk-android:6.8.34'
}