apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.hjq.demo"
        minSdkVersion 14
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 10
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    //APK 签名的那些事：https://www.jianshu.com/p/a1f8e5896aa2
//    signingConfigs {
//        release {
//            keyAlias '密钥别称'
//            keyPassword '密钥密码'
//            storeFile file('E:/MySign.jks')
//            storePassword '密钥库密码'
//        }
//
//        debug {
//            keyAlias '密钥别称'
//            keyPassword '密钥密码'
//            storeFile file('E:/MySign.jks')
//            storePassword '密钥库密码'
//        }
//    }
    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            // 移除无用的resource文件，前提minifyEnabled必须打开
            shrinkResources true
            // ZipAlign优化
            zipAlignEnabled true
            // 设置混淆
            minifyEnabled true
            //加载默认混淆配置涵
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 正式环境签名
            //signingConfig signingConfigs.release
        }

        debug {
            //ZipAlign优化
            zipAlignEnabled false
            //设置混淆
            minifyEnabled false
            //加载默认混淆配置涵
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 开发环境签名
            //signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "default"//这个名字貌似随便取，也可以有多个，总之一定要有
    //多渠道打包
    productFlavors {
        kuan {}     //酷安
        tencent {}  //应用宝
        baidu {}    //百度
        xiaomi {}   //小米
        huawei {}   //华为
        google {}   //谷歌

        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    // JNI 目录
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
}

// api 与 implementation的区别：https://www.jianshu.com/p/8962d6ba936e
dependencies {
    // 依赖 libs 目录下所有 Jar 包
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // 基础库（不包任何第三方框架）
    implementation project(':baselibrary')
    // 示例：添加一个 aar 包
    // implementation(name: 'password_dialog', ext: 'aar')

    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.constraintlayoutVersion"

    // Dex分包，解决 65k 问题
    implementation 'com.android.support:multidex:1.0.3'

    // 状态栏沉浸：https://github.com/gyf-dev/ImmersionBar
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'
    // 侧滑功能：https://github.com/bingoogolapple/BGASwipeBackLayout-Android
    implementation 'cn.bingoogolapple:bga-swipebacklayout:1.1.9@aar'

    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation 'com.hjq:xxpermissions:5.2'
    // 标题栏：https://github.com/getActivity/TitleBar
    implementation 'com.hjq:titlebar:3.5'
    // 吐司工具类：https://github.com/getActivity/ToastUtils
    implementation 'com.hjq:toast:5.0'

    // 圆形的ImageView：https://github.com/hdodenhof/CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // ButterKnife注解库：https://github.com/JakeWharton/butterknife
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeVersion"

    // 友盟统计
    compileOnly 'com.umeng.analytics:analytics:6.1.4'
}

repositories {
    flatDir {
        dirs 'libs' //就是你放aar的目录地址
    }
}